# 第2章 基本的な構文と型

## 出力ウインドウへの書き込み
これまで計算結果を表示するのに`msgbox`関数を使ってきましたが、
`println`関数を使うとMokkosuPadの出力ウインドウに結果を書き込むことができます。
以下のプログラムを実行すると、出力ウインドウに`"Hello, World."`と表示されます。
```
println "Hello, World.";
```

類似の関数として`print`関数があります。
`println`関数は出力後に改行するのに対し、`print`関数は改行しません。
以下は`print`関数の使用例です。
```
print "2 * 3 = ";
println (2 * 3);
```
実行すると、
```
2 * 3 = 6
```
と表示されます。

## 局所変数
関数の定義中にその中でのみ有効な変数を使いたいことがあります。
そのような時は、局所変数を使います。
局所変数はlet式で定義します。

```
let 変数名 = 式1 in 式2
```

let式は`式1`を計算してその結果に名前を付けて`式2`を計算します。
付けた名前は`式2`の中でのみ有効です。

以下は局所変数を使って、xの4乗を求める関数`pow4`を定義した例です。

```
fun pow4 x =
  let x = x * x in
  x * x;
```

この例のようにlet式は既存の定義済みの名前を上書きします。
上のプログラムでlet式の右辺の`x`は引数のx。 
`in`以降の式の`x`はlet式で導入した`x`です。
分かりやすく名前を変えると以下のようになります。

```
fun pow4 x =
  let y = x * x in
  y * y;
```

プログラムの既存の変数を上書きすると、プログラムが読みにくくなる可能性がありますが、
同時に、古い値を参照してしまうのを防止する効果もあります。

let式は以下のように連続して使うことができます。
```
fun f x y =
  let a = x * x in
  let b = y * y in
  a + b;
```

## 浮動小数点数
これまでは整数の演算を扱ってきましたが、次は浮動小数点数を扱ってみましょう。
浮動小数点数をつかうと、`3.14`のような小数点を含む値、
`6.22e23`のようなとても大きな数、
`-1.60e-19`のようなとても小さな数を扱うことが可能になります。

浮動小数点数の型は`Double`型です。

Mokkosuでは`Double`型の演算に整数とは異なる演算子を使います。
`Double`型に関する演算子を以下に示します。

|演算子|意味|
|------|----|
|`式1 +. 式2`|加算|
|`式1 -. 式2`|減算|
|`式1 *. 式2`|乗算|
|`式1 /. 式2`|除算|
|`~-.式`|符号反転|

以下は円の面積を求める関数を定義して使う例です。

```
fun area_of_circle r = r *. r *. pi;

print "半径5.0の円の面積: ";
println (area_of_circle 5.0);
```

実行すると出力ウインドウに以下のように表示されます。

```
area_of_circle : Double -> Double

半径5.0の円の面積: 78.5398163397448
```

浮動小数点数を整数を相互変換するには、
`int_to_double`関数と`double_to_int`関数を使います。

```
int_to_double : Int -> Double
double_to_int : Double -> Int
```

浮動小数点数に関しては、平方根や三角関数など有用な関数が数多く
標準ライブラリに定義されています。
詳細は[リファレンスマニュアルの浮動小数点数の節](../reference/corelib.md#_16)
を参照してください。

### 練習問題 2.1
円錐の体積を求める関数を定義してください。

### 練習問題 2.2
適当な公式を使って円周率πの値を求めるプログラムを書いてください。

## 文字と文字列
`'A'`や`'あ'`のように文字をシングルクォーテーションで囲ったものを
文字定数とよび`Char`型を持ちます。

`Char`型の値は文字コードすなわち整数値と相互変換できます。
```
char_to_int : Char -> Int
int_to_char : Int -> Char
```

`"abc"`のように文字列をダブルクォーテーションで囲ったものを
文字列定数とよび`String`型を持ちます。

文字列は`^`演算子を使って連結できます。

```
let message = "Hello, " ^ "World.";
```

文字列の長さを求めるには`strlen`関数、
文字列中のn番目の文字を取り出すには`strnth`関数を使います。
```
strlen : String -> Int
strnth : String -> Int -> Char
```

## 真偽値
`true`と`false`の2通りの値をもつ型`Bool`が定義されています。
`==`や`<`などの比較演算子の戻り値の型は`Bool`型になります。

`Bool`型に関する演算として、以下のものが定義されています。

|演算|意味|
|----|----|
|式1 && 式2|論理積(かつ)|
|式1 &#124;&#124; 式2|論理和(または)|
|xor 式1 式2|排他的論理和|
|not 式1|否定|

上の表のうち、`&&`と`||`はショートサーキット評価されます。
すなわち、`&&`の場合`式1`が`false`の場合は`式2`の計算は行われず、
`||`の場合`式1`が`true`の場合は`式2`の計算は行われません。

## ユニット
Mokkosuには結果がないことを表す`()`型の値`()`が定義されています。
`println`関数は引数の値を表示しますが、関数自体の戻り値として
何か意味のある値を返す必要はありません。
このような関数の戻り値に`()`型を使います。

例えば`println`の型は以下のようになっています。
```
println : α -> ()
```
ここでαは任意の型を表しています。
`println`は使われ方によって、
```
println : Int -> ()
```
にも、
```
println : String -> ()
```
にもなり、その代表としてαと表記しています。

## do式
do式は以下の構文をした式です。
```
do 式1 in 式2
```
do式は`()`型になる`式1`を計算して、
その後に`式2`を計算しその結果を返します。

以下のプログラムは1から10までの数値を出力ウインドウに表示します。
```
fun print10 n =
  if n > 10 -> ()
  else
    do println n in
    print10 (n + 1);

print10 1;
```
出力結果は以下のようになります。
```
print10 : Int -> ()

1
2
3
4
5
6
7
8
9
10
```

### 練習問題 2.3
`"Hello"`という文字列の各文字を1行に1文字づつ表示するプログラムを作ってください。

### 練習問題 2.4
以下のような掛け算の九九の表を表示するプログラムを作ってください。
```
 1  2  3  4  5  6  7  8  9
 2  4  6  8 10 12 14 16 18
 3  6  9 12 15 18 21 24 27
 4  8 12 16 20 24 28 32 36
 5 10 15 20 25 30 35 40 45
 6 12 18 24 30 36 42 48 54
 7 14 21 28 35 42 49 56 63
 8 16 24 32 40 48 56 64 72
 9 18 27 36 45 54 63 72 81
```


